apply from: "config.gradle"

// 获取当前时间
static def nowDate() {
    return new Date().format("yyyy-MMdd-HHmm", TimeZone.getTimeZone("GMT+8"))
}

ext {
    // Android Application 模块通用依赖
    androidApp = { android, nameSpace, appId ->
        android.with {
            namespace nameSpace
            compileSdk rootProject.ext.android.compileSdkVersion
            defaultConfig {
                applicationId appId
                minSdkVersion rootProject.ext.android.minSdkVersion
                targetSdkVersion rootProject.ext.android.targetSdkVersion
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }
            // todo 添加签名文件配置信息
            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            kotlinOptions {
                jvmTarget = '1.8'
            }
            viewBinding {
                enabled = true
            }
        }
    }

    // Android Lib 模块通用依赖
    androidLib = { android, nameSpace ->
        android.with {
            namespace nameSpace
            compileSdk rootProject.ext.android.compileSdkVersion
            defaultConfig {
                minSdkVersion rootProject.ext.android.minSdkVersion
                targetSdkVersion rootProject.ext.android.targetSdkVersion
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                consumerProguardFiles "consumer-rules.pro"
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            kotlinOptions {
                jvmTarget = '1.8'
            }
            viewBinding {
                enabled = true
            }
        }
    }

    commonLibDependencies = { dependencies ->
        dependencies.with {
            api libs.core_ktx
            api libs.appcompat
            api libs.material
            api libs.constraintlayout

            testImplementation 'junit:junit:4.13.2'
            androidTestImplementation 'androidx.test.ext:junit:1.1.5'
            androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
        }
    }
}